/////////////////////////////////////////////////////////////////////
// Копипаст некоторых методов из OPM

#Использовать tempfiles

Перем мВременныйКаталогУстановки;
Перем Лог;

Функция ПрочитатьМетаданныеПакета(Знач ФайлАрхива) Экспорт
	
	Лог = ОбщегоНазначения.ПолучитьЛог();
	мВременныйКаталогУстановки = ВременныеФайлы.СоздатьКаталог();

	Попытка
		Лог.Отладка("Открываем архив пакета");
		ЧтениеПакета = Новый ЧтениеZipФайла;
		ЧтениеПакета.Открыть(ФайлАрхива);
			
		ФайлМетаданных  = ИзвлечьОбязательныйФайл(ЧтениеПакета, "opm-metadata.xml");
			
		Метаданные = ПрочитатьМетаданные(ФайлМетаданных);
		ИмяПакета = Метаданные.Свойства().Имя;
	Исключение
		ВременныеФайлы.Удалить();
		ВызватьИсключение;
	КонецПопытки;

	ВременныеФайлы.Удалить();
	Возврат Метаданные;
КонецФункции

Функция ПрочитатьМетаданные(Знач ФайлМетаданных)
	
	Перем Метаданные;
	Лог.Отладка("Чтение метаданных пакета");
	Попытка
		Чтение = Новый ЧтениеXML;
		Чтение.ОткрытьФайл(ФайлМетаданных);
		Лог.Отладка("XML загружен");
		Сериализатор = Новый СериализацияМетаданныхПакета;
		Метаданные = Сериализатор.ПрочитатьXML(Чтение);
		
		Чтение.Закрыть();
	Исключение
		Чтение.Закрыть();
		ВызватьИсключение;
	КонецПопытки;
	Лог.Отладка("Метаданные прочитаны");
	
	Возврат Метаданные;
	
КонецФункции

Функция ИзвлечьОбязательныйФайл(Знач Чтение, Знач ИмяФайла)
	ОбщегоНазначения.ПолучитьЛог().Отладка("Извлечение: " + ИмяФайла);
	Элемент = Чтение.Элементы.Найти(ИмяФайла);
	Если Элемент = Неопределено Тогда
		ВызватьИсключение "Неверная структура пакета. Не найден файл " + ИмяФайла;
	КонецЕсли;
	
	Чтение.Извлечь(Элемент, мВременныйКаталогУстановки);
	
	Возврат ОбъединитьПути(мВременныйКаталогУстановки, ИмяФайла);
	
КонецФункции