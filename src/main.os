#Использовать "model"

Процедура ПриНачалеРаботыСистемы()
	
	Если ПустаяСтрока(УправлениеКонтентом.КаталогХраненияПакетов()) Тогда
		ВызватьИсключение "Не задан каталог хранения пакетов OSPX (OSHUB_BINARY_ROOT)";
	КонецЕсли;

	ИспользоватьМаршруты("РегистрацияМаршрутов");
	ИспользоватьСтатическиеФайлы();

	МенеджерБазыДанных.Инициализировать();
	МиграцияДанных.ЗапуститьИмпортВБД();

	ИспользоватьФоновыеЗадания();
 
	// Обновление описаний пакетов
	Расписание = Новый РасписаниеФоновыхЗаданий;
	Расписание.КаждыйЧасВИнтервале(3);
	РегламентныеЗадания.СоздатьПериодическоеЗаданиеПоРасписанию(
		"РегламентныеОперации",
		"ОбновитьОписаниеВсехПакетов",
		Расписание);
	
КонецПроцедуры

Процедура РегистрацияМаршрутов(КоллекцияМаршрутов) Экспорт

	СхемаПакета = Новый Соответствие;
	СхемаПакета.Вставить("controller", "packages");
	СхемаПакета.Вставить("action", "Details");
	КоллекцияМаршрутов.Добавить("package", "package/{id}", СхемаПакета);
	
	СхемаПакета = Новый Соответствие;
	СхемаПакета.Вставить("controller", "download");
	СхемаПакета.Вставить("action", "download");
	КоллекцияМаршрутов.Добавить("stableDownload", "download/{*filepath}", СхемаПакета);
	КоллекцияМаршрутов.Добавить("devDownload", "dev-channel/{*filepath}", СхемаПакета);

	КоллекцияМаршрутов.Добавить("default", "{controller=Home}/{action=Index}/{id?}", Новый Соответствие);

	ОписаниеМаршрута = Новый Соответствие;
	ОписаниеМаршрута.Вставить("controller", "backend");
	ОписаниеМаршрута.Вставить("action", "push");
	КоллекцияМаршрутов.Добавить("push", "push", ОписаниеМаршрута);

КонецПроцедуры