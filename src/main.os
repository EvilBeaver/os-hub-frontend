#Использовать "model"

Процедура ПриНачалеРаботыСистемы()
	
	Если ПустаяСтрока(ПереченьПакетов.КаталогХраненияПакетов()) Тогда
		ВызватьИсключение "Не задан каталог хранения пакетов OSPX (OSHUB_BINARY_ROOT)";
	КонецЕсли;

	ИспользоватьМаршруты("РегистрацияМаршрутов");
	ИспользоватьСтатическиеФайлы();
	
	ПереченьПакетов.Инициализировать("./testHub");
	ИспользоватьФоновыеЗадания();

	// FIXME - API регзаданий будет изменяться
	Расписание = Новый РасписаниеФоновыхЗаданий;
	Расписание.КаждыйЧасВИнтервале(3);

	РегламентныеЗадания.СоздатьПериодическоеЗаданиеПоРасписанию(
		"ПереченьПакетов",
		"ОбновитьКешПакетов",
		Расписание);
	
КонецПроцедуры

Процедура РегистрацияМаршрутов(КоллекцияМаршрутов) Экспорт

	СхемаПакета = Новый Соответствие;
	СхемаПакета.Вставить("controller", "packages");
	СхемаПакета.Вставить("action", "Details");
	КоллекцияМаршрутов.Добавить("package", "package/{id}", СхемаПакета);
	
	СхемаПакета = Новый Соответствие;
	СхемаПакета.Вставить("controller", "download");
	СхемаПакета.Вставить("action", "download");
	КоллекцияМаршрутов.Добавить("stableDownload", "download/{package}/{filename}", СхемаПакета);
	КоллекцияМаршрутов.Добавить("devDownload", "dev-channel/{package}/{filename}", СхемаПакета);

	КоллекцияМаршрутов.Добавить("default", "{controller=Home}/{action=Index}/{id?}", Новый Соответствие);

КонецПроцедуры