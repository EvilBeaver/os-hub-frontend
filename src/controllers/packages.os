#Использовать markdown
#Использовать fs

Функция Index() Экспорт
	
	ДанныеПредставления["Selected"] = 2;	
	Пакеты = УправлениеКонтентом.ТаблицаПакетов();

	Возврат Представление(Пакеты);

КонецФункции

Функция Details() Экспорт
	
	IDПакета = ЗначенияМаршрута["id"];
	ДанныеПакета = УправлениеКонтентом.ДанныеПакетаПоКоду(IDПакета);
	Модель = Новый Структура;
	Модель.Вставить("DescriptionPage", УправлениеКонтентом.ПолучитьСодержимоеReadme(ДанныеПакета.ПутьХранения));
	Модель.Вставить("PackageData", ДанныеПакета);

	Возврат Представление(Модель);

КонецФункции

Функция Search() Экспорт
	Текст = ЗапросHttp.ПараметрыЗапроса()["text"];
	Если ПустаяСтрока(Текст) Тогда
		Возврат ПеренаправлениеНаДействие("Index");
	КонецЕсли;

	ДанныеПредставления["Selected"] = 2;

	Пакеты = УправлениеКонтентом.ТаблицаПакетов();
	НайденныеСтроки = Новый Массив;
	Для Каждого Пакет Из Пакеты Цикл
		Если Найти(Пакет.Название, Текст) > 0 Тогда
			НайденныеСтроки.Добавить(Пакет);
		КонецЕсли;
	КонецЦикла;

	Возврат Представление("Index", НайденныеСтроки);

КонецФункции